#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE CPG_DRV (YDGEO, YDMODEL, YDFIELDS, YDCPG_OPTS, YDVARS, YDGEOMVARS, YDSLHD, YDGPAR, YDTRC, YDRADF, YDXFU, YDCFU, &
& CDCONF, &
& LDSETFSTAT, LDMIXETTLS, YDA_GFLSLP,  PGPSDT2D, YDPGTERM, YDHGRAD, YDFORCESPPT, PGFL, YDA_GFLPC, YDA_GFLPT, &
& YDPHYSMWAVE, YDCPG_SL1, YDCPG_SL2, YDCPG_DDH_TND, YDMF_PHYS_SURF, YDA_RSAVEDP, PGMVT1, PGFLT1, YDA_EXTRA, &
& YDDDH, YDTDDH, YDA_PWRL9, YDA_ZGEO0, YDA_ZRCP0, YDA_ZPRE0F, YDA_ZCTY0, LDWITH_MGRIDS, PTRAJEC, PTRAJEC_OOPS)

!$ACDC toplevel --switch LLPARALLEL

!**** *CPG_DRV* - Grid point calculations driver routine.

!     Purpose.
!     --------
!           Do the parallel loop calling CPG, the grid point calculations.

!**   Interface.
!     ----------
!        *CALL* *CPG_DRV(...)*

!        Explicit arguments :
!        --------------------

!     INPUT:
!     ------
!        CDCONF       : configuration of work
!        LD_DFISTEP   : T if work - dfi
!        LDSETFSTAT, LDMIXETTLS : for the call of CPG_DRV_SETTLOFF
!        YDSL         : SL_STRUCT definition
!        KGPCOMP      : total number of grid points:
!                       ndglg*ndlon in global / ndguxg*ndlon in LAM
!                       used for model computations.
!        PGFLSLP      : GFL array for use in semi-lagrangian physics
!        PGMVTNDHD_DDH: tendencies of horizontal diffusion scheme for GMV.
!        PGFLTNDHD_DDH: tendencies of horizontal diffusion for spectrally treated GFL.
!        PGPSDT2D     : cf. YGPSDT%GP2D in YOMSPSDT (buffer for stochastic physics).

!     INPUT/OUTPUT:
!     -------------
!        PGFL         : unified_treatment grid-point fields at t
!        PGFLPC       : unified_treatment grid-point fields at t (3TL PC only)
!        PGFLPT       : tendency of X variable from phy.
!        PSP_SB       : soil prognostic quantities for the different reservoirs
!        PSP_SG       : surface snow prognostic quantities
!        PSP_RR       : surface prognostic quantities
!        PSP_CL       : cls diagnostic quantities fields
!        PSD_VF       : climatological/geographical diagnostic fields
!        PSD_VP       : deep soil diagnostic fields
!        PSD_VV       : vegetation diagnostic fields
!        PSD_VH       : convective cloud diagnostic fields
!        PSD_VK       : convective cloud prognostic fields
!        PSD_VA       : aerosol diagnostic fields
!        PSD_VC       : climatological ozone profiles diagnostic fields
!        PSD_VD       : diagnostic fields
!        PSD_SFL      : SFLUX diagnostic fields
!        PSD_SFO      : SFORC diagonstic fields
!        PSD_PF       : precip fraction
!        PGRADPHY    : horizontal gradients for 3D physics
!        PSD_XP       : precipitation type diagnostic
!        PSD_XP2      : precipitation type diagnostic
!        YDPGTERM     : pressure gradient quantities, only used if LPGFSF=T
!        PEMTD        : downward longwave emissivity
!        PEMTU        : upward longwave emissivity
!        PTRSW        : shortwave transmissivity
!        PRMOON       : Moon radiation
!        PGMVTNDSI_DDH: tendencies of semi-implicit scheme.
!        PGDEOSI      : DESCENDING INCREMENTAL OPTICAL DEPTHS, SOLAR
!        PGUEOSI      : ASCENDING  INCREMENTAL OPTICAL DEPTHS, SOLAR
!        PGMU0        : COSINE OF SOLAR ZENITH ANGLE, APPROXIMATE ACTUAL VALUE
!        PGMU0_MIN    : COSINE OF SOLAR ZENITH ANGLE, MIN VALUE
!        PGMU0_MAX    : COSINE OF SOLAR ZENITH ANGLE, MAX VALUE
!        PGDEOTI      : descending incremental optical depths, dB/dT(T0) weights
!        PGDEOTI2     : descending incremental optical depths, B weights with
!                       linear T_e correction
!        PGUEOTI      : ascending incremental optical depths, dB/dT(T0) weights
!        PGUEOTI2     : ascending incremental optical depths, B weights with
!                       linear T_e correction
!        PGEOLT       : local optical depths, dB/dT(T0) weights
!        PGEOXT       : maximum optical depths for EBL-EAL, dB/dT(T0) weights
!        PGRPROX      : correction term for adjacent exchanges
!        PGMIXP       : non-statistical weights for bracketing
!        PGFLUXC      : out of bracket part of EBL, resp. EBL-EAL flux
!        PGRSURF      : corrective ratio for surface CTS contribution
!        YDDDH        : diagnostic superstructure
!        YDPHYSMWAVE  : MARYTEMP: mwave structure
!     OUTPUT:
!     -------
!        PB1          : "SLBUF1" buffer for interpolations in SL scheme.
!        PB2          : "SLBUF2" buffer.
!        PGMVT1       : upper air GMV variables buffer.
!        PGMVT1S      : surface GMV variables buffer.
!        PGFLT1       : GFL variables buffer.
!        PGMVTNDSL_DDH: GMV(t+dt,F)-GMV(t or t-dt,O) for DDH
!        PGFLNDSL_DDH : GFL(t+dt,F)-GFL(t or t-dt,O) for DDH

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ARPEGE documentation vol 2 ch 1 and vol 3 ch 6

!     Author.
!     -------
!      Tomas Wilhelmsson  *ECMWF*
!      Original : 2012-03-06 Created from CPG.

! Modifications
! -------------
!     T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!     2013-11, J. Masek: Passing intermittency arrays for ACRANEB2.
!     F. Vana  28-Nov-2013 : Redesigned trajectory handling.
!     M. Diamantakis Dec 13 Add code for LSETTLSVF=T option
!     M. Ahlgrimm Apr 2014: Add lake variables and precip fraction to DDH oon to DDH output
!     K. Yessad (July 2014): Move some variables.
!     K. Yessad (Dec 2016): Prune obsolete options.
!     2017-09, J. Masek: Shifted dimensioning of PGMU0.
!     R. El Khatib 05-Jun-2018 computation of periods moved from cnt4 (OOPS refactoring)
!     M. Diamantakis (June 2018): Extra arguments for multiple (Atlas) grid advection scheme
!     J. Vivoda (July 2018): mixed NESC/SETTLS scheme.
!     R. El Khatib 27-02-2019 Use pointer function SC2PRG to against bounds violation
!     F. Vana June-2020: Simplifying SLAVEPP dataflow
!     I. Polichtchouk (June 2020): add code for LPGFSF option
!     F. Vana (Oct-2020): LPGREUSE
!     R. El Khatib 26-Jul-2022 Fix location of FIRSTPRIVATE directives
!     R. El Khatib 21-Aug-2023 Fix new bounds violations after the refactoring for GPUs
!     A. Alias (Aug 2023) : XIOS IO server setup
!     R. El Khatib 17-Jul-2024 Add missing call to YLCPG_DYN9_%UPDATE_VIEW (Credit : Sipearl)
!     R. El Khatib 09-Dec-2024 Additional portability fix for Intel compiler ifx
! End Modifications
!-------------------------------------------------------------------------------
USE YOMACDRAG
USE YOMDATA
USE YOMMP0

USE TYPE_MODEL              , ONLY : MODEL
USE YOMXFU                  , ONLY : TXFU
USE YOMCFU                  , ONLY : TCFU
USE GEOMETRY_MOD            , ONLY : GEOMETRY
USE PARKIND1                , ONLY : JPIM, JPRB
USE YOMHOOK                 , ONLY : DR_HOOK, JPHOOK, LHOOK
USE YOMLUN                  , ONLY : NULOUT
USE YOMCT0                  , ONLY : NFRGDI
USE YOMCT1                  , ONLY : LGPDIAG
USE YOMCT3                  , ONLY : NSTEP
USE EINT_MOD                , ONLY : SL_STRUCT
USE YOMTRAJ                 , ONLY : TRAJ_PHYS_TYPE, TRAJ_SLAG_TYPE, TRAJ_TYPE
USE YOMTRAJ_OOPS            , ONLY : TRAJ_TYPE_OOPS

USE DDH_MIX                 , ONLY : TYP_DDH
USE TPG_TYPE_MOD            , ONLY : TPG_TYPE
USE SC2PRG_MOD              , ONLY : SC2PRG
USE FIELD_VARIABLES_MOD     , ONLY : FIELD_VARIABLES
USE YOMGEOMVARS,              ONLY : TGEOMVARS         
USE MF_PHYS_SURFACE_TYPE_MOD, ONLY : MF_PHYS_SURF_TYPE
USE CPG_TYPE_MOD            , ONLY : CPG_DDH_TYPE, CPG_DYN_TYPE, CPG_MISC_TYPE, CPG_PHY_TYPE, CPG_TND_TYPE
USE YOMGPAR                 , ONLY : TGPAR
USE CPG_SL1_TYPE_MOD        , ONLY : CPG_SL1B_TYPE, CPG_SL1F_TYPE
USE CPG_SL2_TYPE_MOD        , ONLY : CPG_SL2_TYPE
USE YOMSLHD                 , ONLY : TSLHD          
USE CPG_OPTS_TYPE_MOD       , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE MF_PHYS_TYPE_MOD        , ONLY : MF_PHYS_TYPE
USE YOMDGRADIENT_TYPE_MOD   , ONLY : GRADIENT_TYPE
USE CPG_DDH_TND_TYPE_MOD    , ONLY : CPG_DDH_TND_TYPE
USE FIELDS_MOD              , ONLY : FIELDS
USE YOMGPDDH                , ONLY : TGPDDH
USE FIELD_ARRAY_MODULE
USE YOMTDDH                 , ONLY : TTDDH
USE YOE_PHYS_MWAVE          , ONLY : TEPHYSMWAVE
USE YOMXIOS                 , ONLY : LXIOS
USE YOMFORCESPPT            , ONLY : TFORCESPPT
USE FIELD_ARRAY_UTIL_MODULE
USE FIELD_FACTORY_MODULE
USE YOMRADF                 , ONLY : TRADF
USE YOMTRC                  , ONLY : TTRC

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)          ,INTENT(IN)               :: YDGEO
TYPE(MODEL)             ,INTENT(IN)               :: YDMODEL
TYPE(CPG_OPTS_TYPE)     ,INTENT(IN)               :: YDCPG_OPTS
TYPE(FIELDS)            ,INTENT(INOUT)            :: YDFIELDS
TYPE(TTRC)              ,INTENT(INOUT)            :: YDTRC
TYPE(FIELD_VARIABLES)   ,INTENT(INOUT)            :: YDVARS
TYPE(TSLHD)             ,INTENT(INOUT)            :: YDSLHD
TYPE(TGPAR)             ,INTENT(INOUT)            :: YDGPAR
TYPE(TGEOMVARS)         ,INTENT(IN)               :: YDGEOMVARS
TYPE(TRADF)             ,INTENT(INOUT)            :: YDRADF
TYPE(TXFU)              ,INTENT(INOUT)            :: YDXFU
TYPE(TCFU)              ,INTENT(INOUT)            :: YDCFU
CHARACTER(LEN=1)        ,INTENT(IN)               :: CDCONF
LOGICAL                 ,INTENT(IN)               :: LDSETFSTAT
LOGICAL                 ,INTENT(IN)               :: LDMIXETTLS
TYPE(FIELD_4RB_ARRAY)   ,INTENT(IN)               :: YDA_GFLSLP
REAL(KIND=JPRB)         ,INTENT(IN)               :: PGPSDT2D(YDGEO%YRDIM%NPROMA,YDMODEL%YRML_SPPT%YGPSDT(1)%NG2D,YDGEO%YRDIM%NGPBLKS)
TYPE(TPG_TYPE)          ,INTENT(INOUT)            :: YDPGTERM
TYPE(GRADIENT_TYPE)     ,INTENT(INOUT)            :: YDHGRAD
TYPE(TFORCESPPT)        ,INTENT(INOUT)            :: YDFORCESPPT
REAL(KIND=JPRB)         ,INTENT(INOUT)            :: PGFL(YDGEO%YRDIM%NPROMA,YDGEO%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM,YDGEO%YRDIM%NGPBLKS) 
TYPE(FIELD_4RB_ARRAY)   ,INTENT(INOUT)            :: YDA_GFLPC
TYPE(FIELD_4RB_ARRAY)   ,INTENT(INOUT)            :: YDA_GFLPT
TYPE(TEPHYSMWAVE)       ,INTENT(INOUT)            :: YDPHYSMWAVE
TYPE(CPG_SL1F_TYPE)     ,INTENT(INOUT)            :: YDCPG_SL1
TYPE(CPG_SL2_TYPE)      ,INTENT(INOUT)            :: YDCPG_SL2
TYPE(CPG_DDH_TND_TYPE)  ,INTENT(INOUT)            :: YDCPG_DDH_TND
TYPE(MF_PHYS_SURF_TYPE) ,INTENT(INOUT)            :: YDMF_PHYS_SURF
TYPE(FIELD_4RB_ARRAY)   ,INTENT(INOUT)            :: YDA_RSAVEDP
REAL(KIND=JPRB)         ,INTENT(OUT)              :: PGMVT1(YDGEO%YRDIM%NPROMA,YDGEO%YRDIMV%NFLEVG,YDFIELDS%YRGMV%YT1%NDIM,YDGEO%YRDIM%NGPBLKS) 
REAL(KIND=JPRB)         ,INTENT(OUT)              :: PGFLT1(YDGEO%YRDIM%NPROMA,YDGEO%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM1,YDGEO%YRDIM%NGPBLKS) 
TYPE(FIELD_4RB_ARRAY)   ,INTENT(INOUT)            :: YDA_EXTRA
TYPE(TYP_DDH)           ,INTENT(INOUT)            :: YDDDH
TYPE(TTDDH)             ,INTENT(INOUT)            :: YDTDDH
TYPE(FIELD_3RB_ARRAY)   ,INTENT(INOUT)            :: YDA_PWRL9
TYPE(FIELD_3RB_ARRAY)   ,INTENT(INOUT)            :: YDA_ZGEO0 
TYPE(FIELD_4RB_ARRAY)   ,INTENT(INOUT)            :: YDA_ZRCP0 
TYPE(FIELD_3RB_ARRAY)   ,INTENT(INOUT)            :: YDA_ZPRE0F 
TYPE(FIELD_4RB_ARRAY)   ,INTENT(INOUT)            :: YDA_ZCTY0
LOGICAL                 ,INTENT(IN)               :: LDWITH_MGRIDS
TYPE(TRAJ_TYPE)         ,INTENT(INOUT) ,OPTIONAL  :: PTRAJEC
TYPE(TRAJ_TYPE_OOPS)    ,INTENT(INOUT) ,OPTIONAL  :: PTRAJEC_OOPS

#include "abor1.intfb.h"
#include "cpg.intfb.h"
#include "open_output_lfa.intfb.h"
#include "cpg_drv_settloff.intfb.h"
#include "xias_send_geometry.intfb.h"
#include "xias_update_calendar.intfb.h"
#include "gpnorm_gmv.intfb.h"
#include "gpnorm_gfl.intfb.h"
#include "gpnorm_zb2.intfb.h"

INTEGER :: ILUN

TYPE(FIELD_3IM_ARRAY) :: YLA_ISETTLOFF
TYPE(FIELD_4RB_ARRAY) :: YLA_SAVTEND
TYPE(CPG_DYN_TYPE)    :: YLCPG_DYN0, YLCPG_DYN9
TYPE(CPG_PHY_TYPE)    :: YLCPG_PHY0, YLCPG_PHY9
TYPE(CPG_TND_TYPE)    :: YLCPG_TND
TYPE(CPG_MISC_TYPE)   :: YLCPG_MISC
TYPE(CPG_BNDS_TYPE)   :: YLCPG_BNDS
TYPE(CPG_DDH_TYPE)    :: YLCPG_DDH
TYPE(CPG_SL1B_TYPE)   :: YLCPG_SL1AUX
TYPE(MF_PHYS_TYPE)    :: YLMF_PHYS

LOGICAL :: LLPERSISTENT
LOGICAL :: LLPARALLEL

REAL(KIND=JPHOOK)    :: ZHOOK_HANDLE_INIT
REAL(KIND=JPHOOK)    :: ZHOOK_HANDLE_EXIT
REAL(KIND=JPHOOK)    :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('CPG_DRV', 0, ZHOOK_HANDLE)

ASSOCIATE(YDDIM=>YDGEO%YRDIM, YDDIMV=>YDGEO%YRDIMV, YDGEM=>YDGEO%YRGEM, YDDYN=>YDMODEL%YRML_DYN%YRDYN, YDDYNA=>YDMODEL%YRML_DYN%YRDYNA,  &
&   YDRIP=>YDMODEL%YRML_GCONF%YRRIP, YDPHY=>YDMODEL%YRML_PHY_MF%YRPHY)

ASSOCIATE(NFLEVG=>YDDIMV%NFLEVG,  NCURRENT_ITER=>YDDYN%NCURRENT_ITER,  LMPHYS=>YDPHY%LMPHYS, LSIMPH=>YDMODEL%YRML_PHY_MF%YRSIMPHL%LSIMPH, &
&  YYTTND=>YDDYNA%YYTTND, RSAVEDP=>YDDYN%RSAVEDP, SAVTEND=>YDMODEL%YRML_PHY_G%YRSLPHY%SAVTEND, NGPBLKS=>YDDIM%NGPBLKS, NPROMA=>YDDIM%NPROMA)

LLPARALLEL = .FALSE.

IGPBLKS = YDGEO%YRDIM%NGPBLKS
LLSAVE = (MYPROC == 1) .AND. (NSTEP == 2)


IF (YDMODEL%YRML_PHY_FORCING%LMUSCLFA) THEN
  CALL OPEN_OUTPUT_LFA(YDMODEL%YRML_PHY_MF%YRPHY2, YDMODEL%YRML_PHY_FORCING%NMUSCLFA)
ENDIF

IF (CDCONF == 'A'.OR.CDCONF == 'B') THEN
  CALL XIAS_UPDATE_CALENDAR(NSTEP)
  IF (NSTEP == 0.AND.LXIOS) THEN
        CALL XIAS_SEND_GEOMETRY(YDGEO)
  ENDIF
ENDIF

CALL YLCPG_BNDS%INIT (YDCPG_OPTS)

CALL YDXFU%KEYS_RESET (YDCPG_OPTS%LCONFX, YDCPG_OPTS%LFSTEP, YDMODEL%YRML_GCONF%YRRIP, &
                     & YDMODEL%YRML_PHY_MF%YRPHY, YDMODEL%YRML_AOC%YRMCC)

IF (LHOOK) CALL DR_HOOK('CPG_DRV:INIT',0,ZHOOK_HANDLE_INIT)

IF (YDCPG_OPTS%LSLPHY) THEN
  CALL YLA_SAVTEND%INIT (SAVTEND)
ENDIF

CALL FIELD_NEW (YLA_ISETTLOFF%F_P, UBOUNDS=[NPROMA, NFLEVG, NGPBLKS], PERSISTENT=.TRUE.)

IF (LHOOK) CALL DR_HOOK('CPG_DRV:INIT',1,ZHOOK_HANDLE_INIT)

CALL GSTATS(1025, 0)

!$ACDC COPY, IF=LLPARALLEL {

LLPERSISTENT = LLPARALLEL

CALL YLCPG_DYN0%INIT (0_JPIM, YDFIELDS%REGISTRY, NLEV=NFLEVG, PERSISTENT=LLPERSISTENT, &
                    & YDDYNA=YDMODEL%YRML_DYN%YRDYNA)

CALL YLCPG_PHY0%INIT (0_JPIM, YDFIELDS%REGISTRY, NLEV=NFLEVG, LDMF_PHYS=LMPHYS.OR.LSIMPH, &
                    & PERSISTENT=LLPERSISTENT)

IF (.NOT. YDMODEL%YRML_DYN%YRDYNA%LTWOTL) THEN
  CALL YLCPG_DYN9%INIT (9_JPIM, YDFIELDS%REGISTRY, NLEV=NFLEVG, PERSISTENT=LLPERSISTENT, &
                      & YDDYNA=YDMODEL%YRML_DYN%YRDYNA)
  CALL YLCPG_PHY9%INIT (9_JPIM, YDFIELDS%REGISTRY, NLEV=NFLEVG, LDMF_PHYS=LMPHYS.OR.LSIMPH, &
                      & PERSISTENT=LLPERSISTENT)
ENDIF

CALL YLMF_PHYS%INIT (YDFIELDS%REGISTRY, PERSISTENT=LLPERSISTENT, &
                   & YDCPG_OPTS=YDCPG_OPTS)

CALL YLCPG_MISC%INIT (YDFIELDS%REGISTRY, PERSISTENT=LLPERSISTENT, &
                    & YDCPG_OPTS=YDCPG_OPTS)

CALL YLCPG_SL1AUX%INIT (YDFIELDS%REGISTRY, NLEV=NFLEVG, YDTPTRSLB1=YDMODEL%YRML_DYN%YRPTRSLB1, YGFL=YDFIELDS%YRGFL%YGFL, &
                      & YDMODEL=YDMODEL, PERSISTENT=LLPERSISTENT)

CALL YLCPG_DDH%INIT (YDFIELDS%REGISTRY, NLEV=NFLEVG, YDMODEL=YDMODEL, YDDDH=YDDDH, &
                   & PERSISTENT=LLPERSISTENT)

CALL YLCPG_TND%INIT (YDFIELDS%REGISTRY, NLEV=NFLEVG, YDTTND=YDMODEL%YRML_DYN%YRDYNA%YYTTND, &
                   & PERSISTENT=LLPERSISTENT)

!$ACDC }

IF (LLPARALLEL) THEN

!$ACDC PARALLEL {

  CALL CPG (YDGEO, YLCPG_BNDS, YDCPG_OPTS, YDFORCESPPT, YLCPG_TND, YLCPG_SL1AUX, YDCPG_SL2, YLCPG_MISC, YDGPAR, &
    & YLCPG_PHY0, YLCPG_PHY9, YLMF_PHYS, YDHGRAD, YLCPG_DDH, YLCPG_DYN0, YLCPG_DYN9, YDMF_PHYS_SURF, YDVARS, &
    & YDGEOMVARS, YDXFU, YDCFU, YDMODEL, YDFIELDS, YDTRC, YDRADF, YDA_GFLSLP, YLA_SAVTEND, YDCPG_DDH_TND, YDPGTERM, &
    & YDA_GFLPC, YDA_GFLPT, YLA_ISETTLOFF, YDSLHD, YDCPG_SL1, YDA_EXTRA, YDDDH, YDTDDH, YDA_RSAVEDP, YDA_PWRL9, &
    & YDA_ZGEO0, YDA_ZRCP0, YDA_ZPRE0F, YDA_ZCTY0, LDWITH_MGRIDS, YDPHYSMWAVE)

!$ACDC }

ELSE

!$OMP PARALLEL 
  
  CALL DO_PARALLEL(YDDDH)
  
!$OMP END PARALLEL 

ENDIF

!$ACDC WIPE, IF=LLPARALLEL {

CALL YLCPG_TND%FINAL
CALL YLCPG_DDH%FINAL
CALL YLCPG_SL1AUX%FINAL
CALL YLCPG_MISC%FINAL
CALL YLMF_PHYS%FINAL
CALL YLCPG_PHY9%FINAL
CALL YLCPG_DYN9%FINAL
CALL YLCPG_PHY0%FINAL
CALL YLCPG_DYN0%FINAL

!$ACDC }

CALL GSTATS(1025, 1)

IF (LHOOK) CALL DR_HOOK('CPG_DRV:EXIT', 0, ZHOOK_HANDLE_EXIT)

IF (LDSETFSTAT.OR.LDMIXETTLS) THEN
  CALL CPG_DRV_SETTLOFF(YDGEO,YDMODEL, YLA_ISETTLOFF%F_P%PTR, LDSETFSTAT,LDMIXETTLS)
ENDIF

CALL YLA_ISETTLOFF%FINAL
CALL YLA_SAVTEND%FINAL

IF (LLSAVE) THEN

ILUN = 77

! copy input data for all blocks
OPEN (ILUN, FILE='ACDRAG.IN.dat', FORM='UNFORMATTED')

CALL SAVE(ZZAPRS(:, :, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZAPRSF(:, :, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZDELP(:, :, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZNBVNO(:, :, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZRDELP(:, :, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZU(:, :, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZV(:, :, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZRCORI(:, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZGETRL(:, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZGWDCS(:, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZVRLAN(:, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZVRLDI(:, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZSTRDU(:, :, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZSTRDV(:, :, 1:IGPBLKS - 1), ILUN)
CALL SAVE(ZZRAPTRAJ(:, :, 1:IGPBLKS - 1), ILUN)

CLOSE (ILUN)

! copy output data for all blocks
OPEN (ILUN, FILE='ACDRAG.OUT.dat', FORM='UNFORMATTED')

CALL SAVE(PSTRDU_OUT(:, :, 1:IGPBLKS - 1), ILUN)
CALL SAVE(PSTRDV_OUT(:, :, 1:IGPBLKS - 1), ILUN)
CALL SAVE(PRAPTRAJ_OUT(:, :, 1:IGPBLKS - 1), ILUN)

CLOSE (ILUN)

ENDIF


IF (LHOOK) CALL DR_HOOK('CPG_DRV:EXIT', 1, ZHOOK_HANDLE_EXIT)

IF (LGPDIAG.AND.NFRGDI > 0.AND.MOD(NSTEP,MAX(NFRGDI,1)) == 0) THEN
  WRITE(NULOUT,*) "GPNORM GMVT1 CPG"
  CALL GPNORM_GMV(YDGEO,YDFIELDS%YRGMV,LT1=.TRUE.)
  WRITE(NULOUT,*) "GPNORM GFLT1 CPG"
  CALL GPNORM_GFL(YDGEO,YDFIELDS%YRGFL,LT1=.TRUE.)
  WRITE(NULOUT,*) "GPNORM ZB2 CPG"
  CALL GPNORM_ZB2(YDDIM,YDGEM,YDDYNA,YDMODEL%YRML_DYN%YRPTRSLB2,NFLEVG,YDCPG_SL2%ZDATA)
ENDIF


IF (YDMODEL%YRML_PHY_FORCING%LMUSCLFA) THEN
  CALL LFAFER(YDMODEL%YRML_PHY_FORCING%NMUSCLFA)
ENDIF

!     ------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPG_DRV', 1, ZHOOK_HANDLE)

CONTAINS

SUBROUTINE DO_PARALLEL (YDDDH)

TYPE(TYP_DDH)           ,INTENT(INOUT)            :: YDDDH

TYPE (FIELD_VARIABLES)   :: YLVARS_
TYPE (TGEOMVARS)         :: YLGEOMVARS_
TYPE (MF_PHYS_SURF_TYPE) :: YLMF_PHYS_SURF_
TYPE (CPG_DYN_TYPE)      :: YLCPG_DYN0_
TYPE (CPG_DYN_TYPE)      :: YLCPG_DYN9_
TYPE (CPG_PHY_TYPE)      :: YLCPG_PHY0_
TYPE (CPG_PHY_TYPE)      :: YLCPG_PHY9_
TYPE (CPG_TND_TYPE)      :: YLCPG_TND_
TYPE (TSLHD)             :: YLSLHD_
TYPE (CPG_MISC_TYPE)     :: YLCPG_MISC_
TYPE (CPG_DDH_TYPE)      :: YLCPG_DDH_
TYPE (CPG_SL1B_TYPE)     :: YLCPG_SL1AUX_
TYPE (CPG_SL2_TYPE)      :: YLCPG_SL2_
TYPE (TGPAR)             :: YLGPAR_
TYPE (MF_PHYS_TYPE)      :: YLMF_PHYS_
TYPE (GRADIENT_TYPE)     :: YLHGRAD_
TYPE (CPG_BNDS_TYPE)     :: YLCPG_BNDS_
TYPE (TXFU)              :: YLXFU_
TYPE (TCFU)              :: YLCFU_
TYPE (CPG_DDH_TND_TYPE)  :: YLCPG_DDH_TND_
TYPE (FIELD_4RB_ARRAY)   :: YLA_GFLPC_
TYPE (FIELD_4RB_ARRAY)   :: YLA_GFLPT_
TYPE (FIELD_4RB_ARRAY)   :: YLA_SAVTEND_
TYPE (FIELD_4RB_ARRAY)   :: YLA_GFLSLP_
TYPE (FIELD_4RB_ARRAY)   :: YLA_EXTRA_
TYPE (FIELD_4RB_ARRAY)   :: YLA_RSAVEDP_
TYPE (FIELD_3RB_ARRAY)   :: YLA_PWRL9_
TYPE (TPG_TYPE)          :: YLPGTERM_
TYPE (TYP_DDH)           :: YLDDH_
TYPE (TEPHYSMWAVE)       :: YLPHYSMWAVE_
TYPE (TFORCESPPT)        :: YLFORCESPPT_
TYPE (TTRC)              :: YLTRC_
TYPE (TRADF)             :: YLRADF_
TYPE(FIELD_3RB_ARRAY)    :: YLA_ZGEO0_
TYPE(FIELD_4RB_ARRAY)    :: YLA_ZRCP0_
TYPE(FIELD_3RB_ARRAY)    :: YLA_ZPRE0F_
TYPE(FIELD_4RB_ARRAY)    :: YLA_ZCTY0_
TYPE(FIELD_3IM_ARRAY)    :: YLA_ISETTLOFF_

LOGICAL :: LLUPDATE, LLLAST
INTEGER (KIND=JPIM) :: JKGLO, IBL
TYPE(TRAJ_PHYS_TYPE), POINTER :: ZTRAJEC_PHYS
TYPE(TRAJ_SLAG_TYPE), POINTER :: ZTRAJEC_SLAG
REAL (KIND=JPHOOK) :: ZHOOK_HANDLE_UPDATE_VIEW, ZHOOK_HANDLE_UPDATE_VIEW_1

ASSOCIATE (YDDYNA=>YDMODEL%YRML_DYN%YRDYNA, YDDYN=>YDMODEL%YRML_DYN%YRDYN)

YLVARS_         = YDVARS
YLGEOMVARS_     = YDGEOMVARS
YLMF_PHYS_SURF_ = YDMF_PHYS_SURF
YLCPG_DYN0_     = YLCPG_DYN0
YLCPG_DYN9_     = YLCPG_DYN9
YLCPG_PHY0_     = YLCPG_PHY0
YLCPG_PHY9_     = YLCPG_PHY9
YLCPG_TND_      = YLCPG_TND
YLSLHD_         = YDSLHD
YLCPG_MISC_     = YLCPG_MISC
YLCPG_DDH_      = YLCPG_DDH
YLCPG_SL1AUX_   = YLCPG_SL1AUX
YLCPG_SL2_      = YDCPG_SL2
YLGPAR_         = YDGPAR
YLMF_PHYS_      = YLMF_PHYS
YLHGRAD_        = YDHGRAD
YLCPG_BNDS_     = YLCPG_BNDS
YLXFU_          = YDXFU
YLCFU_          = YDCFU
YLCPG_DDH_TND_  = YDCPG_DDH_TND
YLA_GFLPC_      = YDA_GFLPC
YLA_GFLPT_      = YDA_GFLPT
YLA_SAVTEND_    = YLA_SAVTEND
YLA_GFLSLP_     = YDA_GFLSLP
YLA_EXTRA_      = YDA_EXTRA
YLA_RSAVEDP_    = YDA_RSAVEDP
YLA_PWRL9_      = YDA_PWRL9
YLPGTERM_       = YDPGTERM
YLDDH_          = YDDDH
YLPHYSMWAVE_    = YDPHYSMWAVE
YLFORCESPPT_    = YDFORCESPPT
YLTRC_          = YDTRC
YLRADF_         = YDRADF
YLA_ZGEO0_      = YDA_ZGEO0
YLA_ZRCP0_      = YDA_ZRCP0
YLA_ZPRE0F_     = YDA_ZPRE0F
YLA_ZCTY0_      = YDA_ZCTY0
YLA_ISETTLOFF_  = YLA_ISETTLOFF
  
LLUPDATE = .TRUE.
  
!$OMP DO SCHEDULE(DYNAMIC,1) 
DO JKGLO = 1, YDGEO%YRGEM%NGPTOT_CAP, YDGEO%YRDIM%NPROMA

  IBL=(JKGLO-1)/YDGEO%YRDIM%NPROMA+1

  JBLK = IBL

  LLLAST = JKGLO+YDGEO%YRDIM%NPROMA-1 >= YDGEO%YRGEM%NGPTOT_CAP

  IF (LHOOK) CALL DR_HOOK('CPG_DRV:UPDATE_VIEW', 0, ZHOOK_HANDLE_UPDATE_VIEW)

  IF (LHOOK) CALL DR_HOOK('CPG_DRV:UPDATE_VIEW:YDVARS', 0, ZHOOK_HANDLE_UPDATE_VIEW_1)
  CALL YLVARS_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLGEOMVARS_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  IF (LHOOK) CALL DR_HOOK('CPG_DRV:UPDATE_VIEW:YDVARS', 1, ZHOOK_HANDLE_UPDATE_VIEW_1)
  IF (LHOOK) CALL DR_HOOK('CPG_DRV:UPDATE_VIEW:YLMF_PHYS_SURF', 0, ZHOOK_HANDLE_UPDATE_VIEW_1)
  CALL YLMF_PHYS_SURF_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  IF (LHOOK) CALL DR_HOOK('CPG_DRV:UPDATE_VIEW:YLMF_PHYS_SURF', 1, ZHOOK_HANDLE_UPDATE_VIEW_1)

  IF (LHOOK) CALL DR_HOOK('CPG_DRV:UPDATE_VIEW:OTHER', 0, ZHOOK_HANDLE_UPDATE_VIEW_1)

  CALL YLCPG_DYN0_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLMF_PHYS_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLTRC_%UPDATE_VIEW(BLOCK_INDEX=IBL)
  CALL YLRADF_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLHGRAD_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLGPAR_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLCPG_DDH_TND_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLSLHD_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLXFU_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLCFU_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLA_GFLPC_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLA_GFLPT_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLA_SAVTEND_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLA_GFLSLP_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLA_EXTRA_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLA_RSAVEDP_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLA_PWRL9_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  CALL YLPHYSMWAVE_%UPDATE_VIEW (BLOCK_INDEX=IBL)

  IF (YDMODEL%YRML_PHY_STOCH%YRSTOPH%LFORCENL) THEN
    CALL YLFORCESPPT_%UPDATE_VIEW (BLOCK_INDEX=IBL)
  ENDIF

  IF (LHOOK) CALL DR_HOOK('CPG_DRV:UPDATE_VIEW:OTHER', 1, ZHOOK_HANDLE_UPDATE_VIEW_1)

  IF (LLUPDATE .OR. LLPERSISTENT) THEN
    IF (LHOOK) CALL DR_HOOK('CPG_DRV:UPDATE_VIEW:OTHER.ONCE', 0, ZHOOK_HANDLE_UPDATE_VIEW_1)

    CALL YLCPG_DYN9_%UPDATE_VIEW (BLOCK_INDEX=IBL)
    CALL YLCPG_PHY9_%UPDATE_VIEW (BLOCK_INDEX=IBL)
    CALL YLCPG_PHY0_%UPDATE_VIEW (BLOCK_INDEX=IBL)
    CALL YLCPG_TND_%UPDATE_VIEW (BLOCK_INDEX=IBL)
    CALL YLCPG_MISC_%UPDATE_VIEW (BLOCK_INDEX=IBL)
    CALL YLCPG_SL1AUX_%UPDATE_VIEW (BLOCK_INDEX=IBL)
    CALL YLCPG_DDH_%UPDATE_VIEW (IBL)

    LLUPDATE = .FALSE.
    IF (LHOOK) CALL DR_HOOK('CPG_DRV:UPDATE_VIEW:OTHER.ONCE', 1, ZHOOK_HANDLE_UPDATE_VIEW_1)
  ENDIF

  CALL YLCPG_SL2_%UPDATE_VIEW (IBL)
  CALL YLPGTERM_%UPDATE_VIEW (IBL)

  CALL YLA_ZGEO0_%UPDATE_VIEW (IBL)
  CALL YLA_ZRCP0_%UPDATE_VIEW (IBL)
  CALL YLA_ZPRE0F_%UPDATE_VIEW (IBL)
  CALL YLA_ZCTY0_%UPDATE_VIEW (IBL)
  CALL YLA_ISETTLOFF_%UPDATE_VIEW (IBL)

  CALL YLCPG_BNDS_%UPDATE (IBL)

  IF (LHOOK) CALL DR_HOOK('CPG_DRV:UPDATE_VIEW', 1, ZHOOK_HANDLE_UPDATE_VIEW)


  IF(PRESENT(PTRAJEC))THEN
    CALL SC2PRG(IBL, PTRAJEC%PHYS, ZTRAJEC_PHYS)
    CALL SC2PRG(IBL, PTRAJEC%SLAG, ZTRAJEC_SLAG)
  ELSE
    CALL SC2PRG(IBL, PTRAJEC_OOPS%PHYS, ZTRAJEC_PHYS)
    CALL SC2PRG(IBL, PTRAJEC_OOPS%SLAG, ZTRAJEC_SLAG)
  ENDIF

  CALL CPG (YDGEO, YLCPG_BNDS_, YDCPG_OPTS, YLFORCESPPT_, YLCPG_TND_, YLCPG_SL1AUX_, YLCPG_SL2_, YLCPG_MISC_, YLGPAR_, &
    & YLCPG_PHY0_, YLCPG_PHY9_, YLMF_PHYS_, YLHGRAD_, YLCPG_DDH_, YLCPG_DYN0_, YLCPG_DYN9_, YLMF_PHYS_SURF_, YLVARS_, &
    & YLGEOMVARS_, YLXFU_, YLCFU_, YDMODEL, YDFIELDS, YLTRC_, YLRADF_, YLA_GFLSLP_, YLA_SAVTEND_, YLCPG_DDH_TND_, YLPGTERM_, &
    & YLA_GFLPC_, YLA_GFLPT_, YLA_ISETTLOFF_, YLSLHD_, YDCPG_SL1, YLA_EXTRA_, YLDDH_, YDTDDH, YLA_RSAVEDP_, YLA_PWRL9_, YLA_ZGEO0_, &
    & YLA_ZRCP0_, YLA_ZPRE0F_, YLA_ZCTY0_, LDWITH_MGRIDS, YLPHYSMWAVE_, PGPSDT2D (:,:, IBL), PGFL (:,:,:, IBL), &
    & PGMVT1 (:,:,:, IBL), PGFLT1 (:,:,:, IBL), ZTRAJEC_PHYS, ZTRAJEC_SLAG)

  IF (LLLAST) THEN
    YDDDH = YLDDH_
  ENDIF

ENDDO

!$OMP END DO

END ASSOCIATE

END SUBROUTINE DO_PARALLEL

END SUBROUTINE CPG_DRV
